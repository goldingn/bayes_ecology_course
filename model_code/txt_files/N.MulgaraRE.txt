model
{
# Priors
  y ~ dnorm(0, 1.0E-6)
  r0 ~ dnorm(0, 1.0E-6)

# r1 and r2 are interactions terms for time*treament
# chose either zero (no effect), informative priors, or uninformative priors

#  r1 <- 0   # r1 = 0 means no effect of harvesting in 1st post-harvest sampling period
#  r2 <- 0   # r2 = 0 means no effect of harvesting in 2nd post-harvest sampling period
#  r1 ~ dnorm(0, 1.0E-6)  # uninformative priors
#  r2 ~ dnorm(0, 1.0E-6)
  r1 ~ dnorm(-0.74, 19.389)  # informative priors from Masters 1993 - using se for prior (~0.227)
  r2 ~ dnorm(-0.74, 19.389)

  t1 ~ dnorm(0, 1.0E-6)  # fixed time effect for first period after treatment
  t2 ~ dnorm(0, 1.0E-6)  # fixed time effect for second period after treatment
  
  tau ~ dgamma(0.001, 0.001)   # precision of residual
  sdsite ~ dunif(0, 10)
  tausite <- 1 / sdsite / sdsite   # precision of random site effect

  for (i in 1:10)   # for each plot
  {
    re[i] ~ dnorm(0, tausite)  # random plot (i.e., site) effect

# Treat[] is 0 for control sites and 1 for treatment sites

# calculate mean expectation for each plot in each of the 3 periods
# post-treatment means are calculated as differences from pre-treament means
# accounts for treatment, site and tiem effects
# t1 and t2 are the time effects
# r1 and r2 are the post treatment effects, equivalent to the time x treatment in Masters et al. (2003)
    mean0[i] <- r0*Treat[i] + y + re[i]        # the mean capture rate in each plot prior to the treatment 
    mean1[i] <- mean0[i] + r1*Treat[i] + t1    # mean capture rate in each plot in the first period after the treatment
    mean2[i] <- mean0[i] + r2*Treat[i] + t2    # mean capture rate in each plot in the second period after the treatment

# Observed data, ln(x+1) transformed
    Before[i] ~ dnorm(mean0[i], tau)
    After1[i] ~ dnorm(mean1[i], tau)
    After2[i] ~ dnorm(mean2[i], tau)
  }

# Back-transformed predictions follow
  meanC0 <- exp(y) - 1                       # mean in control plots before treatment
  meanC1 <- exp(y + t1) - 1         # mean in control plots after (t=1)
  meanC2 <- exp(y + t2) - 1         # mean in control plots after (t=2)
  meanT0 <- exp(r0 + y) - 1                # mean in treatment plots before treatment
  meanT1 <- exp(r0 + y + r1 + t1) - 1  # mean in treatment plots after (t=1)
  meanT2 <- exp(r0 + y + r2 + t2) - 1  # mean in treatment plots after (t=2)
}

# Inits for r1 and r2 <> 0
list(tau=100, y=0, r0=0, r1=0, r2=0, t1=0, t2=0, sdsite=0.1)

# Inits for r1 and r2 = 0
list(tau=100, y=0, r0=0, t1=0, t2=0, sdsite=0.1)


Data provided by Pip Masters from:
Masters, P., Dickman, C.R. and Crowther, M. (2003) Effects of cover reduction on mulgara Dasycercus cristicauda (Marsupialia: Dasyuridae), rodent and invertebrate populations in central Australia: implications for land management. Austral Ecology 28: 658-665.

Treat[]	Before[]	After1[]	After2[]
0	2.833213344	1.609437912	2.48490665
0	1.791759469	2.197224577	2.079441542
0	3.044522438	2.708050201	3.135494216
0	2.772588722	1.791759469	2.197224577
0	1.098612289	1.609437912	2.63905733
1	2.944438979	0.693147181	1.791759469
1	2.564949357	0.693147181	1.791759469
1	2.564949357	1.609437912	1.609437912
1	0.693147181	1.098612289	1.098612289
1	1.609437912	0	1.098612289
END


Uninformative priors
	Dbar	Dhat	pD	DIC	
After1	15.278	10.945	4.333	19.611	
After2	11.895	7.531	4.364	16.260	
Before	22.153	18.350	3.803	25.956	
total	49.326	36.826	12.500	61.826	
	 node	 mean	 sd	 MC error	2.5%	median	97.5%	start	sample
	r1	-0.9298	0.5102	0.001666	-1.944	-0.9285	0.0811	1001	100000
	r2	-0.797	0.5082	0.001588	-1.803	-0.7971	0.2187	1001	100000

Informative priors
	Dbar	Dhat	pD	DIC	
After1	14.503	10.601	3.902	18.404	
After2	10.805	6.865	3.940	14.745	
Before	21.588	18.333	3.255	24.843	
total	46.896	35.799	11.097	57.992	
	 node	 mean	 sd	 MC error	2.5%	median	97.5%	start	sample
	r1	-0.7789	0.1996	6.761E-4	-1.169	-0.779	-0.3877	1001	100000
	r2	-0.7364	0.2001	6.473E-4	-1.127	-0.7369	-0.3437	1001	100000

No effect
	Dbar	Dhat	pD	DIC	
After1	16.134	12.699	3.436	19.570	
After2	12.511	9.048	3.463	15.974	
Before	24.260	21.221	3.038	27.298	
total	52.905	42.968	9.937	62.842	
